&ACCESS RVO
&REL 4

;Author: Anil Kircaliali

DEF  A6Rewind ( )
   ;FOLD INI;%{PE}
      ;FOLD BASISTECH INI
         INT Z
         GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
         INTERRUPT ON 3 
         BAS (#INITMOV,0 )
         
      ;ENDFOLD (BASISTECH INI)
      ;FOLD USER INI
         ;Make your modifications here
         
      ;ENDFOLD (USER INI)
      ;ENDFOLD (INI)
      
   $VEL_AXIS[6] = 10
   $ACC_AXIS[6] = 10
   $APO.CPTP = 10
   $APO.CDIS = 10
   
   PTP $AXIS_ACT

   curA6real = $AXIS_ACT.A6
   
   MsgNotify("Current A6 value: %1", "Anil", curA6real)
   
   IF curA6real == 0 THEN
      RETURN
   ENDIF
   
   IF curA6real > 0 THEN
      LOOP
         MsgNotify("Current A6 value: %1, winding down", "Anil", curA6real)
         IF curA6real > 360 THEN
            PTP_REL {A6 -180} C_PTP C_DIS
         ELSE
            IF curA6real >= 180 THEN
               PTP_REL {A6 -179} C_PTP C_DIS
            ELSE
               PTP {A6 0} C_DIS
            ENDIF
         ENDIF
      
         curA6real = $AXIS_ACT.A6
         
         IF curA6real == 0 THEN
            EXIT
         ENDIF
      ENDLOOP
      
   ELSE
      LOOP
         MsgNotify("Current A6 value: %1, winding up", "Anil", curA6real)
         IF curA6real < -360 THEN
            PTP_REL {A6 180} C_PTP C_DIS
         ELSE
            IF curA6real <= -180 THEN
               PTP_REL {A6 179} C_PTP C_DIS
            ELSE
               PTP {A6 0} C_PTP C_DIS
            ENDIF
         ENDIF

         curA6real = $AXIS_ACT.A6

         IF curA6real == 0 THEN
            EXIT
         ENDIF
      ENDLOOP
   ENDIF

END
